＃ver0.30.0
＃日本語処理

＠JP構文制御s
（nswitch,（Ａ０）,倒置文,主題化文,疑問代名詞文,諾否疑問文,連用修飾,補助動詞,希望文,否定文,過去形,終助詞付加）


＠名詞句
（名詞）
（連体修飾句）

＠連体修飾句
（call,動詞文生成処理,（ぽいんた生成）,1,0,0,0,0,2,2,2,2,0）
（call,形容詞文生成処理,（ぽいんた生成）,1,0,0,0,0,2,2,2,2,0,0）
（call,形容詞文生成処理,（ぽいんた生成）,1,0,0,0,0,2,2,2,2,0,1）
（call,動詞文生成処理,（ぽいんた生成）,0,0,0,0,2,2,2,2,2,0）こと
（名詞_連体）の（名詞）

＠補語文リスト
「（らんだむ短文）」
『（らんだむ短文）』
〈（らんだむ短文）〉

＠補語事リスト
（call,動詞文生成処理,（ぽいんた生成）,0,0,0,0,2,2,2,2,2,0）こと
（call,形容詞文生成処理,（ぽいんた生成）,0,0,0,0,2,2,2,2,2,0,2）こと

＠意向文
（感動詞）\n（二人称b）、（call,動詞,意向）（choice,う,うよ）
（感動詞）\n（call,動詞,意向）（choice,う,うよ）、（二人称b）


＃目次：名詞関連
＃ひらカナ変換の都合でちょっとややこしい処理をやっています。
＃総数を足したり足さなかったりした後whenlistで分岐させるやつは頻用しているので覚えて帰ってほしい。
＃ちなみに目的は均等に分岐させることです（合成単語群にできないときのための方法）
＠名詞
（nop,
	（set,めも_あ,（単語群「名詞_連体」の数）
				+（単語群「名詞_動作性」の数）
				+（単語群「名詞_形動」の数）
				+（単語群「名詞_形動化的な」の数）
				+（単語群「名詞_行動」の数）
				+（単語群「名詞_色々な色」の数）
				+（単語群「名詞_所」の数）
				+（単語群「名詞_所_特殊」の数）
				+（単語群「名詞_時」の数）
				+（単語群「名詞_時_特殊」の数）
				+（単語群「名詞_概念」の数）
				+（単語群「名詞_物体」の数）
				+（単語群「名詞_形状」の数）
				+（単語群「名詞_入れ物」の数）
				+（単語群「名詞_食品_料理」の数）
				+（単語群「名詞_食品_素材」の数）
				+（単語群「名詞_食品_液体」の数）
	）
	（set,めも_い,（単語群「名詞_野菜と果物」の数）
				+（単語群「名詞_植物」の数）
				+（単語群「名詞_水中の生き物」の数）
				+（単語群「名詞_陸の生き物」の数）
				+（単語群「名詞_色々な生き物」の数）
				+（単語群「名詞_岩石」の数）
				+（単語群「名詞_鉱物」の数）
	）
	（set,めも_う,（めも_あ）+（めも_い））
）（when,（call,語生成ふらぐ処理,0）＝＝0
	,（whenlist,（乱数1〜（めも_う））
		,＜＝（めも_あ）,（名詞_処理なし）
		,（名詞_ひらカナ処理）
	）
	,（語生成名詞）
）


＠名詞_処理なし
（set,名詞置き場,
	（合成単語群
		,名詞_連体
		,名詞_動作性
		,名詞_形動
		,名詞_形動化的な
		,名詞_行動
		,名詞_色々な色
		,名詞_所
		,名詞_時
		,名詞_時_特殊
		,名詞_概念
		,名詞_物体
		,名詞_形状
		,名詞_入れ物
		,名詞_食品_料理
		,名詞_食品_液体
		,名詞_食品_素材
	）
）（名詞置き場）

＠名詞_ひらカナ処理
（set,名詞置き場,
	（合成単語群
		,名詞_野菜と果物
		,名詞_植物
		,名詞_水中の生き物
		,名詞_陸の生き物
		,名詞_色々な生き物
		,名詞_岩石
		,名詞_鉱物
	）
）（choice,（hira2kata,（名詞置き場））,（名詞置き場））


＃ちょっと呼び出し用
＠場所名詞
（when,（call,語生成ふらぐ処理,0）＝＝0
	,（合成単語群,名詞_所,名詞_所_特殊,名詞_入れ物）
	,（語生成名詞）
）

＠時間名詞
（when,（call,語生成ふらぐ処理,0）＝＝0
	,（合成単語群,名詞_時,名詞_時_特殊）
	,（語生成名詞）
）


＠語生成名詞
（call,語生成ぽーたる,（乱数3〜5））


＃目次：動詞関連
＃動詞単体を活用するだけにする
＃（call,動詞,未然）
＃Ａ０：活用形
＠動詞
（call,動詞活用,（動詞リスト）,（Ａ０））

＃補語も読み込むようにする
＃（call,述語用動詞読み込み,ぽいんた）
＃Ａ０：ぽいんた
＠述語用動詞読み込み
（call,述語用動詞保存,（動詞リスト）,（Ａ０））

＃ぽいんた依存で各種パラメータ読み込んで返します
＃（call,述語用動詞保存,（動詞リスト）,ぽいんた）
＠述語用動詞保存
（nop,
	（call,A_dim,（Ａ５）,（Ａ５）_動詞活用種別,（Ａ０））
	（call,A_dim,（Ａ５）,（Ａ５）_動詞語幹,（Ａ１））
	（call,A_dim,（Ａ５）,（Ａ５）_動詞活用語尾,（Ａ２））
	（call,A_dim,（Ａ５）,（Ａ５）_動詞必須補語,（Ａ３））
	（call,A_dim,（Ａ５）,（Ａ５）_動詞追加補語,（Ａ４））
）

＃return：五段,あそ,ぶ,補必_名が,補追_場で_時間
＠動詞リスト
（when、（call、語生成ふらぐ処理、2）＝＝0
	、（when、（でばっぐふらぐA）＝＝0
		、（動詞一般リスト）
		、（動詞でばっぐリスト）
	）
	、（語生成動詞）
）

＠語生成動詞
サ変複合,（call,語生成ぽーたる,（乱数2〜3））,する,補必_名が,補追_名を_場で_時間
五段,（call,語生成ぽーたる,（乱数2〜4））,（語生成五段）,補必_名が,補追_名を_場で_時間
上下一段,（call,語生成ぽーたる,（乱数1〜4））,る,補必_名が,補追_名を_場で_時間

＠語生成五段
う
く
ぐ
す
つ
ぬ
ぶ
む
る


＃活用を活用種別と活用形、末尾子音に合わせて呼び出すやつ。
＃もっと賢い書き方があると思われる。活用形も引数で呼び出したいんだけどめんどくさくなった。未来の私、がんばってくれ。
＃なんとかしたつもり。だめかも。
＃（call,動詞活用,（動詞リスト）,未然）
＃あるいは（call,動詞活用,五段,あそ,ぶ,補必_名が,補追_場で_時間,未然）
＠動詞活用
（whenlist,（Ａ０）＝＝
	,五段,（whenlist,（Ａ５）＝＝
		,未然,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）わ
			,く,（Ａ１）か
			,ぐ,（Ａ１）が
			,す,（Ａ１）さ
			,つ,（Ａ１）た
			,ぬ,（Ａ１）な
			,ぶ,（Ａ１）ば
			,む,（Ａ１）ま
			,る,（Ａ１）ら
			）
		,連用a,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）い
			,く,（Ａ１）き
			,ぐ,（Ａ１）ぎ
			,す,（Ａ１）し
			,つ,（Ａ１）ち
			,ぬ,（Ａ１）に
			,ぶ,（Ａ１）び
			,む,（Ａ１）み
			,る,（Ａ１）り
			）
		,連用b,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）って
			,く,（Ａ１）いて
			,ぐ,（Ａ１）いで
			,す,（Ａ１）して
			,つ,（Ａ１）って
			,ぬ,（Ａ１）んで
			,ぶ,（Ａ１）んで
			,む,（Ａ１）んで
			,る,（Ａ１）って
			）
		,連用c,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）った
			,く,（Ａ１）いた
			,ぐ,（Ａ１）いだ
			,す,（Ａ１）した
			,つ,（Ａ１）った
			,ぬ,（Ａ１）んだ
			,ぶ,（Ａ１）んだ
			,む,（Ａ１）んだ
			,る,（Ａ１）った
			）
		,連体,（Ａ１）（Ａ２）
		,仮定,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）え
			,く,（Ａ１）け
			,ぐ,（Ａ１）げ
			,す,（Ａ１）せ
			,つ,（Ａ１）て
			,ぬ,（Ａ１）ね
			,ぶ,（Ａ１）べ
			,む,（Ａ１）め
			,る,（Ａ１）れ
			）
		,意向,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）お
			,く,（Ａ１）こ
			,ぐ,（Ａ１）ご
			,す,（Ａ１）そ
			,つ,（Ａ１）と
			,ぬ,（Ａ１）の
			,ぶ,（Ａ１）ぼ
			,む,（Ａ１）も
			,る,（Ａ１）ろ
			）
	）
	,上下一段,（whenlist,（Ａ５）＝＝
		,未然,（Ａ１）
		,連用a,（Ａ１）
		,連用b,（Ａ１）て
		,連用c,（Ａ１）た
		,連体,（Ａ１）る
		,仮定,（Ａ１）れ
		,意向,（Ａ１）よ
	）
	,カ変,（whenlist,（Ａ５）＝＝
		,未然,こ
		,連用a,き
		,連用b,きて
		,連用c,きた
		,連体,くる
		,仮定,くれ
		,意向,こよ
	）
	,サ変,（whenlist,（Ａ５）＝＝
		,未然,し
		,連用a,し
		,連用b,して
		,連用c,した
		,連体,する
		,仮定,すれ
		,意向,しよ
	）
	,サ変複合,（whenlist,（Ａ５）＝＝
		,未然,（Ａ１）し
		,連用a,（Ａ１）し
		,連用b,（Ａ１）して
		,連用c,（Ａ１）した
		,連体,（Ａ１）する
		,仮定,（Ａ１）すれ
		,意向,（Ａ１）しよ
	）
）


＃実質動詞文生成の一部ゾーン
＃前半：必須のやつを辞書から読み出します。
＃後半：必須でもないやつを確率で呼び出して補語リストに追加します。時間句はすべてをめんどくさくさせる。
＃前もって同じぽいんたで動詞を読み込んでおく必要があるらしい
＃Ａ０：ぽいんた
＃（call,動詞補語セットメイン,（Ａ０））
＠動詞補語セットメイン
（nop,
	（call,A_dim,（Ａ０）,（Ａ０）_補語数,（split,（（Ａ０）_動詞必須補語）,_,0,0）-1）
	（for,1,（（Ａ０）_補語数）,1
		,（call,A_dim,（Ａ０）
			,（Ａ０）_補語識別子（Ｃ０）
			,（Ｓ（Ｃ０））
		）
	）

	（for,1,（split,（（Ａ０）_動詞追加補語）,_,0,0）-1,1,
		（when,（乱数1〜7）＝＝1,
			（when,（Ｓ（Ｃ０））＝＝時間,（set,（Ａ０）_時間句存在,1））
			（set,（Ａ０）_補語数,（（Ａ０）_補語数）+1）
			（call,A_dim,（Ａ０）,（Ａ０）_補語識別子（（Ａ０）_補語数）,（Ｓ（Ｃ０）））
		）
	）
）

＃＠めも
＃
＃大体時間句が悪い
＃
＃格助詞部の切り分け。
＃ここは多分補語処理のとこでやったほうがいい。
＃複数ある場合はsplitから乱数で一つ選択。（区切りは「ｌ」（Ｌ全角小文字））
＃
＃名詞選択。
＃一文字目の名詞種別で分岐させてから、疑問詞句番号で疑問名詞と分岐。
＃
＃格助詞部の付与。
＃1.末尾（＝＝句番号が補語数と一致）なら格助詞は要らない。
＃2.疑問詞句番号なら主題化なし。時間句以外は格助詞のまま。
＃3.not末尾、not疑問詞句なら「は」を付与したり置換したり時間句がフリーダムだったりする。
＃
＃完。
＃（call,補語識別処理,（（Ａ０）_補語識別子（Ｃ０））,（（Ａ０）_句番号_Temp）,（Ａ０）,（ぽいんた生成）））
＃（call,補語識別処理,名が,c00_句1,c00,e99）
＃Ａ０：補語識別子：名が
＃Ａ１：句番号：c00_句1
＃Ａ２：文生成の上位ぽいんた：c00
＃Ａ３：補語処理のぽいんた：e99：ここできえる
＠補語識別処理
（when,（Ａ１）！＝（Ａ２）_句（（Ａ２）_補語数）
	,（when,（count,（Ａ０）,ｌ）＝＝0
		,（call,A_dim,（Ａ３）
			,（Ａ３）_格助詞
			,（erase_first,（Ａ０）,（at,（Ａ０）,0））
		）
		,（call,A_dim,（Ａ３）
			,（Ａ３）_格助詞数
			,（split,（erase_first,（Ａ０）,（at,（Ａ０）,0））,ｌ）-1
		）
		（call,A_dim,（Ａ３）
			,（Ａ３）_格助詞
			,（Ｓ（乱数0〜（（Ａ３）_格助詞数）））
		）
	）
）φ
（whenlist,（at,（Ａ０）,0）＝＝
	,名,（when,（Ａ１）！＝（Ａ２）_句（（Ａ２）_疑問詞句番号）
			,（when,（構文_連体修飾頻度_JP）＞＝（乱数1〜100）,（連体修飾句）,（名詞））
			,（choice,（疑問_what）,（疑問_who））
		）
	,場,（when,（Ａ１）！＝（Ａ２）_句（（Ａ２）_疑問詞句番号）
			,（場所名詞）
			,（疑問_where）
		）
	,時,（when,（Ａ１）！＝（Ａ２）_句（（Ａ２）_疑問詞句番号）
			,（時間名詞）
			,（疑問_when）
		）
	,文,（when,（Ａ１）！＝（Ａ２）_句（（Ａ２）_疑問詞句番号）
			,（補語文リスト）
			,（疑問_what）
		）
	,事,（when,（Ａ１）！＝（Ａ２）_句（（Ａ２）_疑問詞句番号）
			,（補語事リスト）
			,（疑問_what）
		）
	,（when,（Ａ１）！＝（Ａ２）_句（（Ａ２）_疑問詞句番号）
			,（名詞）
			,（choice,（疑問_what）,（疑問_who））
	）
）φ
（when,（Ａ１）！＝（Ａ２）_句（（Ａ２）_補語数）
	,（when,（Ａ１）！＝（Ａ２）_句（（Ａ２）_疑問詞句番号）
		,（whenlist,（（Ａ３）_格助詞）＝＝
			,間,（when,（Ａ１）＝＝（Ａ２）_句（（Ａ２）_主題化句番号）,には,、）
			,が,（when,（Ａ１）＝＝（Ａ２）_句（（Ａ２）_主題化句番号）,（when,（（Ａ２）_倒置文フラグ）＝＝0,は,が）,が）
			,を,（when,（Ａ１）＝＝（Ａ２）_句（（Ａ２）_主題化句番号）,は,を）
			,（（Ａ３）_格助詞）（when,（Ａ１）＝＝（Ａ２）_句（（Ａ２）_主題化句番号）,は）
		）
		,（whenlist,（（Ａ３）_格助詞）＝＝
			,間,
			,（（Ａ３）_格助詞）
		）
	）
）（call,A_dispose,（Ａ３））


＃笑いたくなかった
＃「笑い続けたくなかった」あるいは「笑っていたくはなかった」的構文を追加
＃助動詞の定義がやばいので国文法の助動詞と言語学の助動詞全部入れました。
＃本当は処理が楽だったからです。
＃まずは各助動詞を使用するかの判定
＃後ろに来る分から順に、活用形指定に従って処理、結果を格納、次の活用形を指定。のおおよそループ。

＃（call,助動詞活用_動詞文,否定,未然）
＃助動詞の活用だよ。（Ａ０）が助動詞種別で（Ａ１）が活用形です。
＠助動詞活用_動詞文
（whenlist,（Ａ０）＝＝
	,希望,（whenlist,（Ａ１）＝＝
		,未然,たく
		,連用a,たく
		,連用c,たかった
		,連体,たい
	）
	,否定,（whenlist,（Ａ１）＝＝
		,未然,なく
		,連用a,なく
		,連用c,なかった
		,連体,ない
	）
）

＠補助動詞活用_動詞文
（when,（乱数1〜3）＝＝1
	,（call,動詞活用,（補助動詞A）,（Ａ０））（set,（Ａ１）_活用形予約_動詞文,連用a）
	,（call,動詞活用,（補助動詞B）,（Ａ０））（set,（Ａ１）_活用形予約_動詞文,連用b）
）




＃目次：文章生成

＃逆転の発想、主題句を動かすのではなく先にシャッフルして左端の句を主題句にする
＃
＃目次：動詞文
＃
＃処理メモ
＃あとでかく
＃どうせ9割同じ処理なので動詞文と動詞疑問文が分かれているのを何とかしたい。した。
＃一行目はもろもろの初期化。てか英語の方と同じで関数にしたい。した。
＃
＃助動詞処理をやる。動詞を呼び出して活用する前にこっちをすませないと活用形がわからない。
＃メイン述語をオンにして動詞呼び出し→補語格納→動詞活用→格納。終わったらメイン述語をオフにしないと死ぬ。
＃この後の補語処理が疑問文と動詞文で違ったが気合でまとめた。
＃
＃述語、時間句の句番号は先に指定したのでそれを除いた句番号をランダムソートリストに入れる
＃補語（述語は含まれない）を1番から処理していく
＃識別子から名詞を呼び出して、助詞をくっつけて、ランダムな句番号に入れる
＃ここで主題化句番号にあたった句は主題化する（疑問句でない場合のみ）
＃時間の句番号は（ryなので事前に指定した番号に入れる
＃文末に来た時、疑問番号に当たった時は少し処理を分岐させる
＃一応リストをきれいにしておく
＃
＃述語句と疑問句が重なった時は突然動詞を上書きする。
＃述語句に動詞と助動詞を詰める。ここの2行今更感がある。
＃句を0から順番に呼び出せば完成。疑問文ではの？とか？を忘れないように。
＃（call,動詞文,ぽいんた,倒置文,主題化文,疑問代名詞文,諾否疑問文,連用修飾,補助動詞,希望文,否定文,過去形,終助詞付加）
＠動詞文
（call,動詞文生成処理,（ぽいんた生成）,2,2,2,2,2,2,2,2,2,2）

＠動詞文生成処理
（nop,
	（nop,初期処理。各種フラグの読み込みと生成）
	（call,A_dim,（Ａ０）,（Ａ０）_時間句存在,0）
	（call,A_dim,（Ａ０）,（Ａ０）_時間句番号,999）
	（call,A_dim,（Ａ０）,（Ａ０）_疑問詞句番号,999）
	（call,A_dim,（Ａ０）,（Ａ０）_主題化句番号,999）
	（for,1,10,1,
		（call,A_dim,（Ａ０）
			,（Ａ０）_（call,JP構文制御s,（Ｃ０））フラグ
			,（whenlist,（Ａ（Ｃ０））
				,＝＝0,0
				,＝＝1,1
				,（when,（構文_（call,JP構文制御s,（Ｃ０））頻度_JP）＞＝（乱数1〜100）,1,0）
			）
		）
	）

	（nop,末尾助動詞部分の生成。後ろに来る方から順番に有無を判定して処理。
		（c00_助動詞_Part_（それぞれの名前））に必要なパーツを生成して代入しておく。
		わらって 【←連用a/b要求：補助動詞】しまい 【←連用a要求：希望】たく 【←未然要求：否定】な 【←連用c要求：過去】かった）
	（call,A_dim,（Ａ０）
		,（Ａ０）_活用形予約_動詞文
		,（when,（（Ａ０）_過去形フラグ）＝＝1,連用c,連体）
	）
	（call,A_dim,（Ａ０）
		,（Ａ０）_助動詞_Part_否定文
		,（when,（（Ａ０）_否定文フラグ）＝＝1
			,（call,助動詞活用_動詞文,否定,（（Ａ０）_活用形予約_動詞文））
			（set,（Ａ０）_活用形予約_動詞文,未然）
		）
	）
	（call,A_dim,（Ａ０）
		,（Ａ０）_助動詞_Part_希望文
		,（when,（（Ａ０）_希望文フラグ）＝＝1
			,（call,助動詞活用_動詞文,希望,（（Ａ０）_活用形予約_動詞文））
			（set,（Ａ０）_活用形予約_動詞文,連用a）
		）
	）
	（call,A_dim,（Ａ０）
		,（Ａ０）_助動詞_Part_補助動詞,
		（when,（（Ａ０）_補助動詞フラグ）＝＝1
			,（call,補助動詞活用_動詞文,（（Ａ０）_活用形予約_動詞文）,（Ａ０））
		）
	）

	（nop,述語の読み込み→補語セット→動詞の活用）
	（call,A_dim,（Ａ０）,（Ａ０）_メイン述語,1）
	（call,述語用動詞読み込み,（Ａ０））
	（call,動詞補語セットメイン,（Ａ０））

	（call,A_dim,（Ａ０）
		,（Ａ０）_動詞出力_動詞文
		,（call,動詞活用,（（Ａ０）_動詞活用種別）,（（Ａ０）_動詞語幹）,（（Ａ０）_動詞活用語尾）,補必_名が,補追_なし,（（Ａ０）_活用形予約_動詞文））
	）
	（set,（Ａ０）_メイン述語,0）

	（nop,特殊句番号の処理）
	（call,A_dim,（Ａ０）
		,（Ａ０）_述語句番号
		,（when,（（Ａ０）_倒置文フラグ）＝＝0,（（Ａ０）_補語数）,（（Ａ０）_補語数）-1）
	）
	（when,（（Ａ０）_時間句存在）＝＝1,
		（set,（Ａ０）_時間句番号,
			（when,（（Ａ０）_倒置文フラグ）＝＝0
				,0
				,（choice,0,0,0,（（Ａ０）_補語数））
			）
		）
	）
	（when,（（Ａ０）_疑問代名詞文フラグ）＝＝1,
		（set,（Ａ０）_疑問詞句番号,
			（whenlist,（乱数1〜100）
				,＜＝100,（乱数0〜（calc,（（Ａ０）_補語数）-1））
				,（（Ａ０）_補語数）
			）
		）
	）
	（when,（（Ａ０）_主題化文フラグ）＝＝1
		,（set,（Ａ０）_主題化句番号,
			（when,（（Ａ０）_時間句存在）＝＝1
				,（choice,0,1,1,1）
				,0
			）
		）
	）
	
	（nop,補語と句の処理）
	（for,0,（（Ａ０）_補語数）,1,
		（when,（Ｃ０）！＝（（Ａ０）_述語句番号）
			,（when,（Ｃ０）！＝（（Ａ０）_時間句番号）
				,（単語の追加、（Ａ０）_ランダムソートリスト、（Ａ０）_句（Ｃ０））
			）
		）
	）
	（for,1,（（Ａ０）_補語数）,1,
		（whenlist,（（Ａ０）_補語識別子（Ｃ０））＝＝
			,時間,
				（set,（Ａ０）_句番号_Temp,（Ａ０）_句（（Ａ０）_時間句番号））
				（set,（（Ａ０）_句番号_Temp）
					,（call,補語識別処理
						,（（Ａ０）_補語識別子（Ｃ０））
						,（（Ａ０）_句番号_Temp）
						,（Ａ０）
						,（ぽいんた生成））
				）
			,
				（set,（Ａ０）_句番号_Temp,（（Ａ０）_ランダムソートリスト））
				（追加単語の削除、（Ａ０）_ランダムソートリスト、（（Ａ０）_句番号_Temp））
				（set,（（Ａ０）_句番号_Temp）
					,（call,補語識別処理
						,（（Ａ０）_補語識別子（Ｃ０））
						,（（Ａ０）_句番号_Temp）
						,（Ａ０）
						,（ぽいんた生成）
					）
				）
		）
	）（set,（Ａ０）_句番号_Temp,）
	（追加単語の全削除、（Ａ０）_ランダムソートリスト）

	（nop,疑問代名詞句が述語にあたった場合の置換処理。）
	（when,（（Ａ０）_疑問代名詞文フラグ）＝＝1
		,（when,（（Ａ０）_述語句番号）＝＝（（Ａ０）_疑問詞句番号）
			,（set,（Ａ０）_動詞出力_動詞文,（call,疑問動詞,（（Ａ０）_活用形予約_動詞文）））
		）
	）

	（nop,助動詞も併せて述語句を完成させる）
	（set,（Ａ０）_句（（Ａ０）_述語句番号）
		,（when,（（Ａ０）_連用修飾フラグ）＝＝1,（用言修飾部））
		（（Ａ０）_動詞出力_動詞文）
		（for,1,3,1
			,（when,（（Ａ０）_（nswitch,（Ｃ０）,補助動詞,希望文,否定文）フラグ）＝＝1
			,（（Ａ０）_助動詞_Part_（nswitch,（Ｃ０）,補助動詞,希望文,否定文）））
		）
	）
）（nop,nopの処理ここまで。以下、句番号順に呼び出し→終端処理→dispose→おわり）φ
（for,0,（（Ａ０）_補語数）,1,（（Ａ０）_句（Ｃ０））（set,（Ａ０）_句（Ｃ０）,））φ
（when,（（Ａ０）_倒置文フラグ）＝＝0
	,（when,（（Ａ０）_疑問代名詞文フラグ）＝＝1｜｜（（Ａ０）_諾否疑問文フラグ）＝＝1
		,（choice,の？,？）
		,（when,（（Ａ０）_終助詞付加フラグ）＝＝1,（choice,な,ね,よ,の,さ,のに,よね））
	）
	,（when,（（Ａ０）_疑問代名詞文フラグ）＝＝1｜｜（（Ａ０）_諾否疑問文フラグ）＝＝1
		,？
	）
）（call,A_dispose,（Ａ０））


＃目次：形容詞関連
＃（call,形容詞,未然）
＠形容詞
（call,形容詞リスト,（Ａ０））

＠形容詞リスト
（when,（call,語生成ふらぐ処理,1）＝＝0
	,（call,形容詞活用,（形容詞一般リスト）,（Ａ０））
	,（call,形容詞活用,（語生成形容詞）,（Ａ０））
）

＠語生成形容詞
（call,語生成ぽーたる,（乱数2〜4））,い

＃（call,形容詞活用,うつくし,い）
＠形容詞活用
（whenlist,（Ａ２）＝＝
	,未然,（Ａ０）かろ
	,連用a,（Ａ０）く
	,連用b,（Ａ０）く
	,連用c,（Ａ０）かった
	,連体,（Ａ０）い
	,終止,（Ａ０）い
	,仮定,（Ａ０）けれ
	,語幹,（Ａ０）
）


＃目次：形容動詞関連
＠形容動詞
（call,形容動詞リスト,（Ａ０））

＃（call,形容動詞活用,,な）
＃（call,形容動詞活用,（名詞_形動）,な,未然）
＠形容動詞リスト
（when,（call,語生成ふらぐ処理,1）＝＝0
	,（nop,
		（set,めも_あ,（単語群「形容動詞一般リスト」の数））
		（set,めも_い,（単語群「名詞_形動」の数））
		（set,めも_う,（単語群「名詞_形動化的な」の数））
		（set,めも_え,（めも_あ）+（めも_い））
		（set,めも_お,（めも_う）+（めも_え））
	）（whenlist,（乱数1〜（めも_お））
		,＜＝（めも_あ）,（call,形容動詞活用,（形容動詞一般リスト）,（Ａ０））
		,＜＝（めも_え）,（call,形容動詞活用,（名詞_形動）,な,（Ａ０））
		,（call,形容動詞活用,（名詞_形動化的な）的,な,（Ａ０））
	）
	,（call,形容動詞活用,（語生成形容動詞）,（Ａ０））
）

＠語生成形容動詞
（call,語生成ぽーたる,（乱数2〜4））,な

＠形容動詞活用
（whenlist,（Ａ２）＝＝
	,未然,（Ａ０）だろ
	,連用a,（Ａ０）に
	,連用b,（Ａ０）では
	,連用c,（Ａ０）だった
	,連体,（Ａ０）な
	,終止,（Ａ０）だ
	,仮定,（Ａ０）なら
	,語幹,（Ａ０）
）


＃目次：形容詞文
＃（call,形容詞文生成処理,ぽいんた,倒置文,主題化文,疑問代名詞文,諾否疑問文,連用修飾,補助動詞,希望文,否定文,過去形,終助詞付加,形動選択）
＠形容詞文
（call,形容詞文生成処理,（ぽいんた生成）,2,2,2,2,2,2,2,2,2,2,2）

＠形容詞文生成処理
（nop,
	（call,A_dim,（Ａ０）,（Ａ０）_時間句存在,0）
	（call,A_dim,（Ａ０）,（Ａ０）_時間句番号,999）
	（call,A_dim,（Ａ０）,（Ａ０）_疑問詞句番号,999）
	（call,A_dim,（Ａ０）,（Ａ０）_主題化句番号,999）
	（for,1,10,1,
		（call,A_dim,（Ａ０）
			,（Ａ０）_（call,JP構文制御s,（Ｃ０））フラグ
			,（whenlist,（Ａ（Ｃ０））
				,＝＝0,0
				,＝＝1,1
				,（when,（構文_（call,JP構文制御s,（Ｃ０））頻度_JP）＞＝（乱数1〜100）,1,0）
			）
		）
	）
	（call,A_dim,（Ａ０）
		,（Ａ０）_形動選択フラグ
		,（whenlist,（Ａ１１）
				,＝＝0,0
				,＝＝1,1
				,（choice,0,1）
			）
	）

	（call,A_dim,（Ａ０）
		,（Ａ０）_活用形予約_形容詞文
		,（when,（（Ａ０）_過去形フラグ）＝＝1
			,連用c
			,（when,（（Ａ０）_倒置文フラグ）＝＝1
				,連体
				,（when,（（Ａ０）_諾否疑問文フラグ）＝＝1
					,（when,（（Ａ０）_形動選択フラグ）＝＝1,語幹,終止）
					,（when,（（Ａ０）_終助詞付加フラグ）＝＝1
						,（call,A_dim,（Ａ０）
							,（Ａ０）_終助詞付加分類
							,（when,（（Ａ０）_形動選択フラグ）＝＝0,終止,（choice,語幹,終止,連体）
							）
						）（（Ａ０）_終助詞付加分類）
						,終止
					）
				）
			）
		）
	）
	（call,A_dim,（Ａ０）
		,（Ａ０）_助動詞_Part_否定文
		,（when,（（Ａ０）_否定文フラグ）＝＝1
				,（call,助動詞活用_形容詞文,否定,（（Ａ０）_活用形予約_形容詞文））
				（set,（Ａ０）_活用形予約_形容詞文,連用b）
		）
	）

	（call,A_dim,（Ａ０）
		,（Ａ０）_述語本体入れ_形容詞文
		,（when,（（Ａ０）_連用修飾フラグ）＝＝1,（用言修飾部））
		（call,（when,（（Ａ０）_形動選択フラグ）＝＝0,形容詞,形容動詞）,（（Ａ０）_活用形予約_形容詞文））
		（when,（（Ａ０）_否定文フラグ）＝＝1,（（Ａ０）_助動詞_Part_否定文））
	）
）（when,（（Ａ０）_倒置文フラグ）＝＝1
		,（（Ａ０）_述語本体入れ_形容詞文）（when,（構文_連体修飾頻度_JP）＞＝（乱数1〜100）,（連体修飾句）,（名詞））（when,（（Ａ０）_諾否疑問文フラグ）＝＝1,？）
		,（when,（構文_連体修飾頻度_JP）＞＝（乱数1〜100）,（連体修飾句）,（名詞））（when,（（Ａ０）_主題化文フラグ）＝＝1,は,が）（（Ａ０）_述語本体入れ_形容詞文）
		（when,（（Ａ０）_諾否疑問文フラグ）＝＝1
			,？
			,（when,（（Ａ０）_終助詞付加フラグ）＝＝1
				,（when,（（Ａ０）_形動選択フラグ）＝＝0
					,（choice,な,ね,よ,の,のさ,のに,よね）
					,（whenlist,（（Ａ０）_終助詞付加分類）＝＝
						,語幹,（choice,ね,よ,よね）
						,終止,（choice,な,ね,よ,よね）
						,連体,（choice,の,のさ,のに）
					）
				）
			）
		）
）（call,A_dispose,（Ａ０））


＃（call,助動詞活用_形容詞文,ない,未然,う）
＠助動詞活用_形容詞文
（whenlist,（Ａ０）＝＝
	,否定,（whenlist,（Ａ１）＝＝
		,未然,なく
		,連用a,なく
		,連用c,なかった
		,連体,ない
		,終止,ない
		,語幹,ない
	）
）


＠用言修飾部
（nop,
	（set,めも_あ,（単語群「形容詞一般リスト」の数））
	（set,めも_い,（単語群「形容動詞一般リスト」の数）+（単語群「名詞_形動化的な」の数））
	（set,めも_う,（単語群「副詞リスト」の数））
	（set,めも_あい,（めも_あ）+（めも_い））
	（set,めも_あいう,（めも_あい）+（めも_う））
）（whenlist,（乱数1〜（めも_あいう））
	,＜＝（めも_あ）,（call,形容詞,連用b）
	,＜＝（めも_あい）,（call,形容動詞,連用a）
	,（副詞リスト）
）


＃目次：料理名関連
＠料理
（名詞）の（調味料）（調理A）
（調理A）（名詞）
（名詞）の（調理B）
（名詞）の（調理C）
（名詞）（調理C）
（名詞）の（choice,（名詞_食品_料理）,（名詞_食品_液体））
（名詞）（choice,（名詞_食品_料理）,（名詞_食品_液体））

＠調理A
焼き
蒸し
煮込み
炒め

＠調理B
姿煮
天ぷら

＠調理C
サラダ
フライ


＠調味料
（nop,
	（set,めも_あ,（単語群「名詞_植物」の数））
	（set,めも_い,（単語群「名詞_食品_液体」の数））
	（set,めも_う,（単語群「工夫」の数））
	（set,めも_あい,（めも_あ）+（めも_い））
	（set,めも_あう,（めも_あい）+（めも_う））
）（whenlist,（乱数1〜（めも_あう））
	,＜＝（めも_あ）,（名詞_植物）
	,＜＝（めも_あい）,（名詞_食品_液体）
	,（工夫）
）

＠工夫
照り
包み
炭火

＠食材
（名詞）

＠食材_制限版
（合成単語群
	,名詞_野菜と果物
	,名詞_水中の生き物
	,名詞_陸の生き物
	,名詞_色々な生き物
	,名詞_食品_素材
）


＃目次：語生成
＃うにひらがな
＃3041-3096、3099-309F
＃12353-12438、12441-12447

＃うにカタカナ
＃30A1-30FA、30FB-30FF
＃12449-12538、12539-12543

＃じすひらがな
＃829F-82F1
＃33439-33521

＃じすカタカナ
＃8340-8396
＃33600-33686

＊でばっぐ_しふとじす
\0\b[2]\q[ひらがな,On語生成てすとしゅつりょく,ひらがな]
\q[かたかな,On語生成てすとしゅつりょく,カタカナ]
\q[かんじ,On語生成てすとしゅつりょく,漢字1]


\1（call,めにゅー末尾,でばっぐ）


＊On語生成てすとしゅつりょく
（語生成（Ｒ０））

\q[もっかい,On語生成てすとしゅつりょく,（Ｒ０）]
（call,めにゅー末尾,でばっぐ_しふとじす）


＃目次：ここからでばっぐ用のテスト領域
＊でばっぐ_日本語系
\0\b[2]\![quicksection,true]《でばっぐ_日本語系》
文φ：φ
\q[動詞文,Onでばっぐ_活用テスト汎用,動詞文]　φ
\q[形容詞文,Onでばっぐ_活用テスト汎用,形容詞文]　φ
\q[連体修飾句,Onでばっぐ_活用テスト汎用,連体修飾句]
語φ：φ
\q[名詞,Onでばっぐ_活用テスト汎用,名詞]　φ
\q[動詞,でばっぐ_動詞活用テスト]　φ
\q[形容詞,でばっぐ_形容詞活用テスト]　φ
\q[形容動詞,でばっぐ_形容動詞活用テスト]
その他φ：φ
\q[補語文,Onでばっぐ_活用テスト汎用,補語文リスト]　φ
\q[補語事,Onでばっぐ_活用テスト汎用,補語事リスト]　
\q[料理,Onでばっぐ_活用テスト汎用,料理]　φ
\q[わるだくみの極限,でばっぐ_わるだくみの極限]

（for、1、10、1
	、\![*]\q[（call,JP構文制御s,（Ｃ０））制御,On設定変更_処理,（call,JP構文制御s,（Ｃ０））制御,2,でばっぐ_日本語系]φ：
	（whenlist,（（call,JP構文制御s,（Ｃ０））制御）＝＝,0,オフ,1,オン,2,ランダム）\n
）
\![*]\q[形動選択制御,On設定変更_処理,形動選択制御,2,でばっぐ_日本語系]　φ：（whenlist,（形動選択制御）＝＝,0,形容詞,1,形容動詞,2,ランダム）

\![*]\q[語生成ふらぐ制御,On設定変更_処理,語生成ふらぐ,2,でばっぐ_日本語系]　φ：（whenlist,（語生成ふらぐ）＝＝,0,オフ,1,オン,2,ランダム）
\![*]\q[でばっぐふらぐA,On設定変更_処理,でばっぐふらぐA,1,でばっぐ_日本語系]　φ：（whenlist,（でばっぐふらぐA）＝＝,0,オフ,1,オン）
＿でばっぐ_語生成関連設定

\q[ろぐ,On持ち物表示処理,1,でばっぐ活用ろぐ,_]

\1（call,めにゅー末尾,でばっぐ）


＊でばっぐ_語生成関連設定
\0\b[2]\![quicksection,true]《でばっぐ_日本語系語生成》

\![*]\q[語生成品詞,On設定変更_処理,語生成品詞,3,でばっぐ_語生成関連設定]　φ：（whenlist,（語生成品詞）＝＝,0,名詞,1,形容詞,2,動詞,3,混合）
\![*]\q[語生成文字種,On設定変更_処理,語生成文字種,3,でばっぐ_語生成関連設定]　φ：（whenlist,（語生成文字種）＝＝,0,ひらがな,1,カタカナ,2,漢字,3,混合）
\![*]\q[語生成手加減,On設定変更_処理,語生成手加減,1,でばっぐ_語生成関連設定]　φ：（whenlist,（語生成手加減）＝＝,0,問答無用,1,手加減）

\1（call,めにゅー末尾,でばっぐ_日本語系）

＊Onでばっぐ_活用テスト汎用
\0\b[2]\![quicksection,true]《（Ｒ０）》
（set,でばっぐ活用ろぐ,（でばっぐ活用ろぐ）_（Ｒ０）_（現在タイムスタンプ）（version表示）_倒置主題疑代諾否連用補助希望否定過去終助（for、1、10、1、（（call,JP構文制御s,（Ｃ０））制御））_語生成（語生成ふらぐ）（語生成品詞）（語生成文字種）（語生成手加減）_でばっぐ（でばっぐふらぐA）_予備0_予備1_予備2_予備3）
（for,0,9,1
	,（set,でばっぐ活用ろぐtemp,
		（whenlist,（Ｒ０）＝＝
			,動詞文,（call,動詞文生成処理,（ぽいんた生成）（for、1、10、1、,（（call,JP構文制御s,（Ｃ０））制御）））
			,形容詞文,（call,形容詞文生成処理,（ぽいんた生成）（for、1、10、1、,（（call,JP構文制御s,（Ｃ０））制御））,（形動選択制御））
			,（（Ｒ０））
		）
	）（set,でばっぐ活用ろぐ,（でばっぐ活用ろぐ）_（でばっぐ活用ろぐtemp））（でばっぐ活用ろぐtemp）\n
）
（set,でばっぐ活用ろぐtemp,）
\1\q[もっかい,Onでばっぐ_活用テスト汎用,（Ｒ０）]

（call,めにゅー末尾,でばっぐ_日本語系）


＊でばっぐ_動詞活用テスト
\0\b[2]\![quicksection,true]《動詞活用》

（call,動詞,未然）ない
（call,動詞,連用a）ます
（call,動詞,連用b）
（call,動詞,連用c）
（call,動詞,連体）
（call,動詞,仮定）ば
（call,動詞,意向）う
\1\q[もっかい,でばっぐ_動詞活用テスト]

（call,めにゅー末尾,でばっぐ_日本語系）

＊でばっぐ_形容詞活用テスト
\0\b[2]\![quicksection,true]《形容詞活用》

（call,形容詞,未然）う
（call,形容詞,連用a）
（call,形容詞,連用b）て
（call,形容詞,連用c）
（call,形容詞,連体）
（call,形容詞,終止）
（call,形容詞,仮定）ば
（call,形容詞,語幹）
\1\q[もっかい,でばっぐ_日本語系]

（call,めにゅー末尾,でばっぐ_日本語系）

＊でばっぐ_形容動詞活用テスト
\0\b[2]\![quicksection,true]《形容動詞活用》

（call,形容動詞,未然）う
（call,形容動詞,連用a）
（call,形容動詞,連用b）
（call,形容動詞,連用c）
（call,形容動詞,連体）
（call,形容動詞,終止）
（call,形容動詞,仮定）ば
（call,形容動詞,語幹）
\1\q[もっかい,でばっぐ_形容動詞活用テスト]

（call,めにゅー末尾,でばっぐ_日本語系）


＊でばっぐ_わるだくみの極限
\0\b[2]\![quicksection,true]《極限》
（for,1,（乱数3〜7）,1,
	（語生成
		（whenlist,（語生成文字種）＝＝
			,0,ひらがな
			,1,カタカナ
			,2,漢字1
			,3,（choice,ひらがな,カタカナ,漢字1）
		）
	）
）。


\1\q[もっかい,でばっぐ_わるだくみの極限]

（call,めにゅー末尾,でばっぐ_日本語系）