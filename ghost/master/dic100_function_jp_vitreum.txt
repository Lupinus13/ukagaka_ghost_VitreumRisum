＃ver0.28.2
＃日本語処理


＠名詞句
（名詞）
（連体修飾句）

＠連体修飾句_初期化
（set,倒置フラグ制御,1）（set,疑問フラグ制御,0）（set,be疑問制御,0）（set,語生成ふらぐ,0）

＠連体修飾句
（連体修飾句_初期化）（動詞文）
（連体修飾句_初期化）（set,形容いな制御,0）（形容詞文）
（連体修飾句_初期化）（set,形容いな制御,1）（形容詞文）
（名詞_連体）の（名詞）


＠意向文
（感動詞）\n（二人称b）、（call,動詞,意向）（choice,う,うよ）
（感動詞）\n（call,動詞,意向）（choice,う,うよ）、（二人称b）



＃目次：名詞関連
＃ひらカナ変換の都合でちょっとややこしい処理をやっています。
＃総数を足したり足さなかったりした後whenlistで分岐させるやつは頻用しているので覚えて帰ってほしい。
＃ちなみに目的は均等に分岐させることです（合成単語群にできないときのための方法）
＠名詞
（nop,
	（set,めも_あ,（単語群「名詞_連体」の数）
				+（単語群「名詞_動作性」の数）
				+（単語群「名詞_形動」の数）
				+（単語群「名詞_形動化的な」の数）
				+（単語群「名詞_行動」の数）
				+（単語群「名詞_色々な色」の数）
				+（単語群「名詞_所」の数）
				+（単語群「名詞_時」の数）
				+（単語群「名詞_時_特殊」の数）
				+（単語群「名詞_概念」の数）
				+（単語群「名詞_物体」の数）
				+（単語群「名詞_形状」の数）
				+（単語群「名詞_入れ物」の数）
				+（単語群「名詞_食品_料理」の数）
				+（単語群「名詞_食品_素材」の数）
				+（単語群「名詞_食品_液体」の数）
	）
	（set,めも_い,（単語群「名詞_野菜と果物」の数）
				+（単語群「名詞_植物」の数）
				+（単語群「名詞_水中の生き物」の数）
				+（単語群「名詞_陸の生き物」の数）
				+（単語群「名詞_色々な生き物」の数）
				+（単語群「名詞_岩石」の数）
				+（単語群「名詞_鉱物」の数）
	）
	（set,めも_う,（めも_あ）+（めも_い））
）（when,（call,語生成ふらぐ処理,0）＝＝0
	,（whenlist,（乱数1〜（めも_う））
		,＜＝（めも_あ）,（名詞_処理なし）
		,（名詞_ひらカナ処理）
	）
	,（語生成名詞）
）




＠名詞_処理なし
（set,名詞置き場,
	（合成単語群
		,名詞_連体
		,名詞_動作性
		,名詞_形動
		,名詞_形動化的な
		,名詞_行動
		,名詞_色々な色
		,名詞_所
		,名詞_時
		,名詞_時_特殊
		,名詞_概念
		,名詞_物体
		,名詞_形状
		,名詞_入れ物
		,名詞_食品_料理
		,名詞_食品_液体
		,名詞_食品_素材
	）
）（名詞置き場）

＠名詞_ひらカナ処理
（set,名詞置き場,
	（合成単語群
		,名詞_野菜と果物
		,名詞_植物
		,名詞_水中の生き物
		,名詞_陸の生き物
		,名詞_色々な生き物
		,名詞_岩石
		,名詞_鉱物
	）
）（choice,（hira2kata,（名詞置き場））,（名詞置き場））

＃ちょっと呼び出し用
＠場所名詞
（when,（call,語生成ふらぐ処理,0）＝＝0
	,（合成単語群,名詞_所,場所_特殊,名詞_入れ物）
	,（語生成名詞）
）

＠時間名詞
（when,（call,語生成ふらぐ処理,0）＝＝0
	,（合成単語群,名詞_時,名詞_時_特殊）
	,（語生成名詞）
）



＠語生成名詞
（call,語生成ぽーたる,（乱数3〜5））




＃目次：動詞関連
＠動詞
（set,活用形_動詞,（Ａ０））（動詞リスト）

＠動詞リスト
（when,（call,語生成ふらぐ処理,2）＝＝0
	,（when,（でばっぐふらぐA）＝＝0
		,（call,動詞活用,（動詞一般リスト））
		,（call,動詞活用,（動詞でばっぐリスト））
	）
	,（call,動詞活用,（語生成動詞））
）

＠語生成動詞
サ変複合,（call,語生成ぽーたる,（乱数2〜3））,する,補必_名が,補追_名を_場で_時間
五段,（call,語生成ぽーたる,（乱数2〜4））,（語生成五段）,補必_名が,補追_名を_場で_時間
上下一段,（call,語生成ぽーたる,（乱数1〜4））,る,補必_名が,補追_名を_場で_時間

＠語生成五段
う
く
ぐ
す
つ
ぬ
ぶ
む
る

＃必須のやつを辞書から読み出します。
＃必須でもないやつを確率で呼び出して補語リストに追加します。時間句はすべてをめんどくさくさせる。
＠動詞補語セットメイン
（nop,
	（set,補語数,（split,（Ａ０）,_,0,0）-1）
	（for,1,（補語数）,1,（set,補語識別子（Ｃ０）,（Ｓ（Ｃ０））））

	（for,1,（split,（Ａ１）,_,0,0）-1,1,
		（when,（乱数1〜7）＝＝1,
			（when,（Ｓ（Ｃ０））＝＝時間,（set,時間句存在,1））
			（set,補語数,（補語数）+1）
			（set,補語識別子（補語数）,（Ｓ（Ｃ０）））
		）
	）
）


＃活用を活用種別と活用形、末尾子音に合わせて呼び出すやつ。
＃もっと賢い書き方があると思われる。活用形も引数で呼び出したいんだけどめんどくさくなった。未来の私、がんばってくれ。
＠動詞活用
（when,（メイン述語）＝＝1
	,（call,動詞補語セットメイン,（Ａ３）,（Ａ４））
）（whenlist,（Ａ０）＝＝
	,五段,（whenlist,（活用形_動詞）＝＝
		,未然,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）わ
			,く,（Ａ１）か
			,ぐ,（Ａ１）が
			,す,（Ａ１）さ
			,つ,（Ａ１）た
			,ぬ,（Ａ１）な
			,ぶ,（Ａ１）ば
			,む,（Ａ１）ま
			,る,（Ａ１）ら
			）
		,連用a,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）い
			,く,（Ａ１）き
			,ぐ,（Ａ１）ぎ
			,す,（Ａ１）し
			,つ,（Ａ１）ち
			,ぬ,（Ａ１）に
			,ぶ,（Ａ１）び
			,む,（Ａ１）み
			,る,（Ａ１）り
			）
		,連用b,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）って
			,く,（Ａ１）いて
			,ぐ,（Ａ１）いで
			,す,（Ａ１）して
			,つ,（Ａ１）って
			,ぬ,（Ａ１）んで
			,ぶ,（Ａ１）んで
			,む,（Ａ１）んで
			,る,（Ａ１）って
			）
		,連用c,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）った
			,く,（Ａ１）いた
			,ぐ,（Ａ１）いだ
			,す,（Ａ１）した
			,つ,（Ａ１）った
			,ぬ,（Ａ１）んだ
			,ぶ,（Ａ１）んだ
			,む,（Ａ１）んだ
			,る,（Ａ１）った
			）
		,連体,（Ａ１）（Ａ２）
		,仮定,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）え
			,く,（Ａ１）け
			,ぐ,（Ａ１）げ
			,す,（Ａ１）せ
			,つ,（Ａ１）て
			,ぬ,（Ａ１）ね
			,ぶ,（Ａ１）べ
			,む,（Ａ１）め
			,る,（Ａ１）れ
			）
		,意向,（whenlist,（Ａ２）＝＝
			,う,（Ａ１）お
			,く,（Ａ１）こ
			,ぐ,（Ａ１）ご
			,す,（Ａ１）そ
			,つ,（Ａ１）と
			,ぬ,（Ａ１）の
			,ぶ,（Ａ１）ぼ
			,む,（Ａ１）も
			,る,（Ａ１）ろ
			）
	）
	,上下一段,（whenlist,（活用形_動詞）＝＝
		,未然,（Ａ１）
		,連用a,（Ａ１）
		,連用b,（Ａ１）て
		,連用c,（Ａ１）た
		,連体,（Ａ１）る
		,仮定,（Ａ１）れ
		,意向,（Ａ１）よ
	）
	,カ変,（whenlist,（活用形_動詞）＝＝
		,未然,こ
		,連用a,き
		,連用b,きて
		,連用c,きた
		,連体,くる
		,仮定,くれ
		,意向,こよ
	）
	,サ変,（whenlist,（活用形_動詞）＝＝
		,未然,し
		,連用a,し
		,連用b,して
		,連用c,した
		,連体,する
		,仮定,すれ
		,意向,しよ
	）
	,サ変複合,（whenlist,（活用形_動詞）＝＝
		,未然,（Ａ１）し
		,連用a,（Ａ１）し
		,連用b,（Ａ１）して
		,連用c,（Ａ１）した
		,連体,（Ａ１）する
		,仮定,（Ａ１）すれ
		,意向,（Ａ１）しよ
	）
）

＃目次：文章生成
＠jp生成前_初期化
（nop,
	（set,時間句存在,0）（set,時間句番号,999）（set,疑問詞句番号,999）（set,主題化句番号,999）
	（set,疑問フラグ,
		（whenlist,（疑問フラグ制御）
			,＝＝0,0
			,＝＝1,1
			,（when,（乱数0〜5）＝＝1,1,0）
		）
	）
	（set,倒置フラグ,
		（whenlist,（倒置フラグ制御）
			,＝＝0,0
			,＝＝1,1
			,（choice,0,1）
		）
	）
	（set,主題化フラグ,
		（whenlist,（主題化フラグ制御）
			,＝＝0,0
			,＝＝1,1
			,（when,（乱数0〜3）＝＝1,1,0）
		）
	）
	（set,連用修飾フラグ,
		（whenlist,（連用修飾制御）
			,＝＝0,0
			,＝＝1,1
			,（when,（乱数0〜2）＝＝1,1,0）
		）
	）
	（set,形容いなフラグ,
		（whenlist,（形容いな制御）
			,＝＝0,0
			,＝＝1,1
			,（choice,0,1）
		）
	）
	（set,be疑問フラグ,
		（whenlist,（be疑問制御）
			,＝＝0,0
			,＝＝1,1
			,（when,（乱数0〜5）＝＝1,1,0）
		）
	）
	（for,1,4,1
		,（set,助動詞_（nswitch,（Ｃ０）,補助,たい,ない,過去）フラグ
			,（whenlist,（助動詞_（nswitch,（Ｃ０）,補助,たい,ない,過去）制御）＝＝
				,0,0
				,1,1
				,（when,（乱数0〜（nswitch,（Ｃ０）,5,5,5,3））＝＝1,1,0）
			）
		）
	）
）


＠jp補語文生成
（文制御フラグをランダム化するやつ）（補語文リスト）

＠補語文リスト
「（らんだむ短文）」
『（らんだむ短文）』
〈（らんだむ短文）〉

＠jp補語事生成
（文制御フラグをランダム化するやつ）（nop,（set,補語事生成中,1）（set,倒置フラグ制御,0）（set,主題化フラグ制御,0）（set,疑問フラグ制御,0）（set,be疑問制御,0）（set,助動詞_たい制御,0））（補語事リスト）（set,補語事生成中,0）

＠補語事リスト
（動詞文）こと
（形容詞文）こと

＃逆転の発想、主題句を動かすのではなく先にシャッフルして左端の句を主題句にする
＃
＃目次：動詞文
＃
＃処理メモ
＃あとでかく
＃どうせ9割同じ処理なので動詞文と動詞疑問文が分かれているのを何とかしたい。した。
＃一行目はもろもろの初期化。てか英語の方と同じで関数にしたい。した。
＃
＃助動詞処理をやる。動詞を呼び出して活用する前にこっちをすませないと活用形がわからない。
＃メイン述語をオンにして動詞呼び出し→補語格納→動詞活用→格納。終わったらメイン述語をオフにしないと死ぬ。
＃この後の補語処理が疑問文と動詞文で違ったが気合でまとめた。
＃
＃述語、時間句の句番号は先に指定したのでそれを除いた句番号をランダムソートリストに入れる
＃補語（述語は含まれない）を1番から処理していく
＃識別子から名詞を呼び出して、助詞をくっつけて、ランダムな句番号に入れる
＃ここで主題化句番号にあたった句は主題化する（疑問句でない場合のみ）
＃時間の句番号は（ryなので事前に指定した番号に入れる
＃文末に来た時、疑問番号に当たった時は少し処理を分岐させる
＃一応リストをきれいにしておく
＃
＃述語句と疑問句が重なった時は突然動詞を上書きする。
＃述語句に動詞と助動詞を詰める。ここの2行今更感がある。
＃句を0から順番に呼び出せば完成。疑問文ではの？とか？を忘れないように。
＠動詞文
（nop,
	（jp生成前_初期化）
	（助動詞処理_動詞文）
	（set,メイン述語,1）（set,動詞出力_動詞文,（call,動詞,（活用形予約_動詞文）））（set,メイン述語,0）

	（set,述語句番号,（when,（倒置フラグ）＝＝0,（補語数）,（補語数）-1））
	（when,（時間句存在）＝＝1,
		（set,時間句番号,（when,（倒置フラグ）＝＝0,0,（choice,0,0,0,（補語数））））
	）

	（when,（疑問フラグ）＝＝1,
		（set,めも_い,（補語数）-1）
		（set,疑問詞句番号,
			（whenlist,（乱数1〜100）
				,＜＝100,（乱数0〜（めも_い））
				,（補語数）
			）
		）
	）	

	（when,（主題化フラグ）＝＝1
		,（set,主題化句番号,
			（when,（時間句存在）＝＝1
				,（choice,0,1,1,1）
				,0
			）
		）
	）
	
	（for,0,（補語数）,1,
		（when,（Ｃ０）！＝（述語句番号）,（when,（Ｃ０）！＝（時間句番号）,（単語の追加、ランダムソートリスト、句（Ｃ０））））
	）
	（for,1,（補語数）,1,
		（whenlist,（補語識別子（Ｃ０））＝＝
			,時間,
			（set,めも_あ,句（時間句番号））
			（set,（めも_あ）,（call,補語識別処理,（補語識別子（Ｃ０））,（めも_あ）））
			,
			（set,めも_あ,（ランダムソートリスト））
			（追加単語の削除、ランダムソートリスト、（めも_あ））
			（set,（めも_あ）,（call,補語識別処理,（補語識別子（Ｃ０））,（めも_あ）））
		）
	）
	（追加単語の全削除、ランダムソートリスト）

	（when,（疑問フラグ）＝＝1,（when,（述語句番号）＝＝（疑問詞句番号）,（疑問動詞呼び出し）））
	（set,句（述語句番号）,（when,（連用修飾フラグ）＝＝1,（用言修飾部））（動詞出力_動詞文）（助動詞出力_動詞文））
）（for,0,（補語数）,1,（句（Ｃ０）））φ
（when,（疑問フラグ）＝＝0
	,（when,（倒置フラグ）＝＝0
		,（when,（be疑問フラグ）＝＝1,（choice,の？,？））
		,（when,（be疑問フラグ）＝＝1,？）
	）
	,（when,（倒置フラグ）＝＝0
		,（choice,の？,？）
		,？
	）
）


＠疑問動詞呼び出し
（set,動詞出力_動詞文,（set,活用形_動詞,（活用形予約_動詞文））（疑問動詞））


＃＠めも
＃
＃大体時間句が悪い
＃
＃格助詞部の切り分け。
＃ここは多分補語処理のとこでやったほうがいい。
＃複数ある場合はsplitから乱数で一つ選択。（区切りは「ｌ」（Ｌ全角小文字））
＃
＃名詞選択。
＃一文字目の名詞種別で分岐させてから、疑問詞句番号で疑問名詞と分岐。
＃
＃格助詞部の付与。
＃1.末尾（＝＝句番号が補語数と一致）なら格助詞は要らない。
＃2.疑問詞句番号なら主題化なし。時間句以外は格助詞のまま。
＃3.not末尾、not疑問詞句なら「は」を付与したり置換したり時間句がフリーダムだったりする。
＃
＃完。
＠補語識別処理
（when,（Ａ１）！＝句（補語数）
	,（when,（count,（Ａ０）,ｌ）＝＝0
		,（set,格助詞,（erase_first,（Ａ０）,（at,（Ａ０）,0）））
		,（set,格助詞数末尾,（split,（erase_first,（Ａ０）,（at,（Ａ０）,0））,ｌ）-1）
		（set,格助詞,（Ｓ（乱数0〜（格助詞数末尾））））
	）
）φ
（whenlist,（at,（Ａ０）,0）＝＝
	,名,（when,（Ａ１）！＝句（疑問詞句番号）
			,（名詞）
			,（choice,（疑問_what）,（疑問_who））
		）
	,場,（when,（Ａ１）！＝句（疑問詞句番号）
			,（場所名詞）
			,（疑問_where）
		）
	,時,（when,（Ａ１）！＝句（疑問詞句番号）
			,（時間名詞）
			,（疑問_when）
		）
	,文,（when,（Ａ１）！＝句（疑問詞句番号）
			,（補語文_ストック）（set,文再生成ふらぐ,1）
			,（疑問_what）
		）
	,事,（when,（Ａ１）！＝句（疑問詞句番号）
			,（補語事_ストック）（set,事再生成ふらぐ,1）
			,（疑問_what）
		）
	,（when,（Ａ１）！＝句（疑問詞句番号）
			,（名詞）
			,（choice,（疑問_what）,（疑問_who））
	）
）φ
（when,（Ａ１）！＝句（補語数）
	,（when,（Ａ１）！＝句（疑問詞句番号）
		,（whenlist,（格助詞）＝＝
			,間,（when,（Ａ１）＝＝句（主題化句番号）,には,、）
			,が,（when,（Ａ１）＝＝句（主題化句番号）,は,が）
			,を,（when,（Ａ１）＝＝句（主題化句番号）,は,を）
			,（格助詞）（when,（Ａ１）＝＝句（主題化句番号）,は）
		）
		,（whenlist,（格助詞）＝＝
			,間,
			,（格助詞）
		）
	）
）


＃笑いたくなかった
＃「笑い続けたくなかった」あるいは「笑っていたくはなかった」的構文を追加
＃助動詞の定義がやばいので国文法の助動詞と言語学の助動詞全部入れました。
＃本当は処理が楽だったからです。
＃まずは各助動詞を使用するかの判定
＃後ろに来る分から順に、活用形指定に従って処理、結果を格納、次の活用形を指定。のおおよそループ。
＠助動詞処理_動詞文
（nop,
	（set,活用形予約_動詞文,（when,（助動詞_過去フラグ）＝＝1,連用c,連体））
	（set,助動詞_語_ない,
		（when,（助動詞_ないフラグ）＝＝1
				,（call,助動詞活用_動詞文,ない,（活用形予約_動詞文））
				（set,活用形予約_動詞文,未然）
		）
	）
	（set,助動詞_語_たい,
		（when,（助動詞_たいフラグ）＝＝1
				,（call,助動詞活用_動詞文,たい,（活用形予約_動詞文））
				（set,活用形予約_動詞文,連用a）
		）
	）
	（set,助動詞_語_補助,
		（when,（助動詞_補助フラグ）＝＝1
				,（call,補助動詞活用_動詞文,（活用形予約_動詞文））
		）
	）
）

＃しまっておいた助動詞を順番に表示する部分を切り出しただけ。邪魔だったので。
＠助動詞出力_動詞文
（for,1,3,1,（when,（助動詞_（nswitch,（Ｃ０）,補助,たい,ない）フラグ）＝＝1,（助動詞_語_（nswitch,（Ｃ０）,補助,たい,ない））））

＃（call,助動詞活用_動詞文,ない,未然,う）
＃助動詞の活用だよ。（Ａ０）が動詞種別で（Ａ１）が活用形です。
＠助動詞活用_動詞文
（whenlist,（Ａ０）＝＝
	,たい,（whenlist,（Ａ１）＝＝
		,未然,たく
		,連用a,たく
		,連用c,たかった
		,連体,たい
	）
	,ない,（whenlist,（Ａ１）＝＝
		,未然,なく
		,連用a,なく
		,連用c,なかった
		,連体,ない
	）
）

＠補助動詞活用_動詞文
（set,活用形_動詞,（Ａ０））（when,（乱数1〜3）＝＝1
	,（call,動詞活用,（補助動詞A））（set,活用形予約_動詞文,連用a）
	,（call,動詞活用,（補助動詞B））（set,活用形予約_動詞文,連用b）
）



＃目次：形容詞関連

＃目次：形容動詞関連

＃（call,形容詞,未然）



＠形容詞
（set,活用形_形容詞,（Ａ０））（形容詞リスト）

＠形容詞リスト
（when,（call,語生成ふらぐ処理,1）＝＝0
	,（call,形容詞活用,（形容詞一般リスト））
	,（call,形容詞活用,（語生成形容詞））
）

＠語生成形容詞
（call,語生成ぽーたる,（乱数2〜4））,い

＃（call,形容詞活用,うつくし,い）
＠形容詞活用
（whenlist,（活用形_形容詞）＝＝
	,未然,（Ａ０）かろ
	,連用a,（Ａ０）く
	,連用b,（Ａ０）く
	,連用c,（Ａ０）かった
	,連体,（Ａ０）い
	,終止,（Ａ０）い
	,仮定,（Ａ０）けれ
	,語幹,（Ａ０）
）


＠形容動詞
（set,活用形_形容動詞,（Ａ０））（形容動詞リスト）

＃（call,形容動詞活用,,な）
＃（call,形容動詞活用,（名詞_形動）,な）
＠形容動詞リスト
（when,（call,語生成ふらぐ処理,1）＝＝0
	,（nop,
		（set,めも_あ,（単語群「形容動詞一般リスト」の数））
		（set,めも_い,（単語群「名詞_形動」の数））
		（set,めも_う,（単語群「名詞_形動化的な」の数））
		（set,めも_え,（めも_あ）+（めも_い））
		（set,めも_お,（めも_う）+（めも_え））
	）（whenlist,（乱数1〜（めも_お））
		,＜＝（めも_あ）,（call,形容動詞活用,（形容動詞一般リスト））
		,＜＝（めも_え）,（call,形容動詞活用,（名詞_形動）,な）
		,（call,形容動詞活用,（名詞_形動化的な）的,な）
	）
	,（call,形容動詞活用,（語生成形容動詞））
）

＠語生成形容動詞
（call,語生成ぽーたる,（乱数2〜4））,な

＠形容動詞活用
（whenlist,（活用形_形容動詞）＝＝
	,未然,（Ａ０）だろ
	,連用a,（Ａ０）に
	,連用b,（Ａ０）では
	,連用c,（Ａ０）だった
	,連体,（Ａ０）な
	,終止,（Ａ０）だ
	,仮定,（Ａ０）なら
	,語幹,（Ａ０）
）

＃目次：形容詞文
＠形容詞文
（nop,
	（jp生成前_初期化）
	（助動詞処理_形容詞文）
	（set,述語本体入れ_形容詞文,（when,（連用修飾フラグ）＝＝1,（用言修飾部））（call,（when,（形容いなフラグ）＝＝0,形容詞,形容動詞）,（活用形予約_形容詞文））（when,（助動詞_ないフラグ）＝＝1,（助動詞_語_ない）））
）（when,（倒置フラグ）＝＝1
		,（述語本体入れ_形容詞文）（名詞）（when,（be疑問フラグ）＝＝1,？）
		,（名詞）（when,（主題化フラグ）＝＝1,は,が）（述語本体入れ_形容詞文）（when,（be疑問フラグ）＝＝1,？）
）

＠助動詞処理_形容詞文
（nop,
	（set,活用形予約_形容詞文,
		（when,（補語事生成中）＝＝1
			,連体
			,（when,（助動詞_過去フラグ）＝＝1
				,連用c
				,（when,（倒置フラグ）＝＝1
					,連体
					,（when,（be疑問フラグ）＝＝1＆＆（形容いなフラグ）＝＝1
						,語幹
						,終止
					）
				）
			）
		）
	）
	（set,助動詞_語_ない,
		（when,（助動詞_ないフラグ）＝＝1
				,（call,助動詞活用_形容詞文,ない,（活用形予約_形容詞文））（set,活用形予約_形容詞文,連用b）
		）
	）
）

＃（call,助動詞活用_形容詞文,ない,未然,う）
＠助動詞活用_形容詞文
（whenlist,（Ａ０）＝＝
	,ない,（whenlist,（Ａ１）＝＝
		,未然,なく
		,連用a,なく
		,連用c,なかった
		,連体,ない
		,終止,ない
		,語幹,ない
	）
）



＠用言修飾部
（nop,
	（set,めも_あ,（単語群「形容詞一般リスト」の数））
	（set,めも_い,（単語群「形容動詞一般リスト」の数）+（単語群「名詞_形動化的な」の数））
	（set,めも_う,（単語群「副詞リスト」の数））
	（set,めも_あい,（めも_あ）+（めも_い））
	（set,めも_あいう,（めも_あい）+（めも_う））
）（whenlist,（乱数1〜（めも_あいう））
	,＜＝（めも_あ）,（call,形容詞,連用b）
	,＜＝（めも_あい）,（call,形容動詞,連用a）
	,（副詞リスト）
）

＃目次：料理名関連
＠料理
（食材）の（調味料）（調理）
（食材）の（choice,（名詞_食品_料理）,（名詞_食品_液体））
焼き（名詞_野菜と果物）

＠調理
焼き
蒸し
煮込み
炒め

＠調味料
（nop,
	（set,めも_あ,（単語群「名詞_植物」の数））
	（set,めも_い,（単語群「名詞_食品_液体」の数））
	（set,めも_う,（単語群「工夫」の数））
	（set,めも_あい,（めも_あ）+（めも_い））
	（set,めも_あう,（めも_あい）+（めも_う））
）（whenlist,（乱数1〜（めも_あう））
	,＜＝（めも_あ）,（名詞_植物）
	,＜＝（めも_あい）,（名詞_食品_液体）
	,（工夫）
）

＠工夫
照り
包み
炭火


＠食材
（合成単語群
	,名詞_野菜と果物
	,名詞_水中の生き物
	,名詞_陸の生き物
	,名詞_色々な生き物
	,名詞_食品_素材
）

＃目次：語生成
＃うにひらがな
＃3041-3096、3099-309F
＃12353-12438、12441-12447

＃うにカタカナ
＃30A1-30FA、30FB-30FF
＃12449-12538、12539-12543

＃じすひらがな
＃829F-82F1
＃33439-33521

＃じすカタカナ
＃8340-8396
＃33600-33686



＊でばっぐ_しふとじす
\0\b[2]\q[ひらがな,On語生成てすとしゅつりょく,ひらがな]
\q[かたかな,On語生成てすとしゅつりょく,カタカナ]
\q[かんじ,On語生成てすとしゅつりょく,漢字1]


\1（call,めにゅー末尾,でばっぐ）


＊On語生成てすとしゅつりょく
（語生成（Ｒ０））

\q[もっかい,On語生成てすとしゅつりょく,（Ｒ０）]
（call,めにゅー末尾,でばっぐ_しふとじす）


＃目次：ここからでばっぐ用のテスト領域
＊でばっぐ_日本語系
\0\b[2]\![quicksection,true]《でばっぐ_日本語系》

文φ：φ
\q[動詞文,Onでばっぐ_活用テスト汎用,動詞文]　φ
\q[形容詞文,Onでばっぐ_活用テスト汎用,形容詞文]　φ
\q[連体修飾句,Onでばっぐ_活用テスト汎用,連体修飾句]
語φ：φ
\q[名詞,Onでばっぐ_活用テスト汎用,名詞]　φ
\q[動詞,でばっぐ_動詞活用テスト]　φ
\q[形容詞,でばっぐ_形容詞活用テスト]　φ
\q[形容動詞,でばっぐ_形容動詞活用テスト]
その他φ：φ
\q[補語文,Onでばっぐ_活用テスト汎用,jp補語文生成]　φ
\q[補語事,Onでばっぐ_活用テスト汎用,jp補語事生成]　
\q[料理,Onでばっぐ_活用テスト汎用,料理]　φ
\q[わるだくみの極限,でばっぐ_わるだくみの極限]

\![*]\q[倒置フラグ制御,On設定変更_処理,倒置フラグ制御,2,でばっぐ_日本語系]　φ：（whenlist,（倒置フラグ制御）＝＝,0,オフ,1,オン,2,ランダム）
\![*]\q[疑問フラグ制御,On設定変更_処理,疑問フラグ制御,2,でばっぐ_日本語系]　φ：（whenlist,（疑問フラグ制御）＝＝,0,オフ,1,オン,2,ランダム）
\![*]\q[主題化フラグ制御,On設定変更_処理,主題化フラグ制御,2,でばっぐ_日本語系]　φ：（whenlist,（主題化フラグ制御）＝＝,0,オフ,1,オン,2,ランダム）
\![*]\q[連用修飾制御,On設定変更_処理,連用修飾制御,2,でばっぐ_日本語系]　φ：（whenlist,（連用修飾制御）＝＝,0,なし,1,あり,2,ランダム）
\![*]\q[形容いな制御,On設定変更_処理,形容いな制御,2,でばっぐ_日本語系]　φ：（whenlist,（形容いな制御）＝＝,0,形容詞,1,形容動詞,2,ランダム）
\![*]\q[be疑問制御,On設定変更_処理,be疑問制御,2,でばっぐ_日本語系]　φ：（whenlist,（be疑問制御）＝＝,0,なし,1,あり,2,ランダム）
\![*]\q[助動詞_補助制御,On設定変更_処理,助動詞_補助制御,2,でばっぐ_日本語系]　φ：（whenlist,（助動詞_補助制御）＝＝,0,なし,1,あり,2,ランダム）
\![*]\q[助動詞_たい制御,On設定変更_処理,助動詞_たい制御,2,でばっぐ_日本語系]　φ：（whenlist,（助動詞_たい制御）＝＝,0,なし,1,あり,2,ランダム）
\![*]\q[助動詞_ない制御,On設定変更_処理,助動詞_ない制御,2,でばっぐ_日本語系]　φ：（whenlist,（助動詞_ない制御）＝＝,0,なし,1,あり,2,ランダム）
\![*]\q[助動詞_過去制御,On設定変更_処理,助動詞_過去制御,2,でばっぐ_日本語系]　φ：（whenlist,（助動詞_過去制御）＝＝,0,なし,1,あり,2,ランダム）

\![*]\q[語生成ふらぐ制御,On設定変更_処理,語生成ふらぐ,2,でばっぐ_日本語系]　φ：（whenlist,（語生成ふらぐ）＝＝,0,オフ,1,オン,2,ランダム）
\![*]\q[でばっぐふらぐA,On設定変更_処理,でばっぐふらぐA,1,でばっぐ_日本語系]　φ：（whenlist,（でばっぐふらぐA）＝＝,0,オフ,1,オン）
＿でばっぐ_語生成関連設定

\1（call,めにゅー末尾,でばっぐ）


＊でばっぐ_語生成関連設定
\0\b[2]\![quicksection,true]《でばっぐ_日本語系語生成》

\![*]\q[語生成品詞,On設定変更_処理,語生成品詞,3,でばっぐ_語生成関連設定]　φ：（whenlist,（語生成品詞）＝＝,0,名詞,1,形容詞,2,動詞,3,混合）
\![*]\q[語生成文字種,On設定変更_処理,語生成文字種,3,でばっぐ_語生成関連設定]　φ：（whenlist,（語生成文字種）＝＝,0,ひらがな,1,カタカナ,2,漢字,3,混合）
\![*]\q[語生成手加減,On設定変更_処理,語生成手加減,1,でばっぐ_語生成関連設定]　φ：（whenlist,（語生成手加減）＝＝,0,問答無用,1,手加減）

\1（call,めにゅー末尾,でばっぐ_日本語系）


＊Onでばっぐ_活用テスト汎用
\0\b[2]\![quicksection,true]《（Ｒ０）》

（for,0,10,1,（（Ｒ０））\n）
\1\q[もっかい,Onでばっぐ_活用テスト汎用,（Ｒ０）]

（call,めにゅー末尾,でばっぐ_日本語系）


＊でばっぐ_動詞活用テスト
\0\b[2]\![quicksection,true]《動詞活用》

（call,動詞,未然）ない
（call,動詞,連用a）ます
（call,動詞,連用b）
（call,動詞,連用c）
（call,動詞,連体）
（call,動詞,仮定）ば
（call,動詞,意向）う
\1\q[もっかい,でばっぐ_動詞活用テスト]

（call,めにゅー末尾,でばっぐ_日本語系）

＊でばっぐ_形容詞活用テスト
\0\b[2]\![quicksection,true]《形容詞活用》

（call,形容詞,未然）う
（call,形容詞,連用a）
（call,形容詞,連用b）て
（call,形容詞,連用c）
（call,形容詞,連体）
（call,形容詞,終止）
（call,形容詞,仮定）ば
（call,形容詞,語幹）
\1\q[もっかい,でばっぐ_日本語系]

（call,めにゅー末尾,でばっぐ_日本語系）

＊でばっぐ_形容動詞活用テスト
\0\b[2]\![quicksection,true]《形容動詞活用》

（call,形容動詞,未然）う
（call,形容動詞,連用a）
（call,形容動詞,連用b）
（call,形容動詞,連用c）
（call,形容動詞,連体）
（call,形容動詞,終止）
（call,形容動詞,仮定）ば
（call,形容動詞,語幹）
\1\q[もっかい,でばっぐ_形容動詞活用テスト]

（call,めにゅー末尾,でばっぐ_日本語系）


＊でばっぐ_わるだくみの極限
\0\b[2]\![quicksection,true]《極限》
（for,1,（乱数3〜7）,1,
	（語生成
		（whenlist,（語生成文字種）＝＝
			,0,ひらがな
			,1,カタカナ
			,2,漢字1
			,3,（choice,ひらがな,カタカナ,漢字1）
		）
	）
）。


\1\q[もっかい,でばっぐ_わるだくみの極限]

（call,めにゅー末尾,でばっぐ_日本語系）